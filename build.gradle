plugins {
  id "com.gradle.plugin-publish" version "0.9.7"
}
plugins {
  id 'net.researchgate.release' version '2.3.4'
}

[
        "groovy",
        "java-gradle-plugin",
        "maven",
        "maven-publish",
        "idea"
].each {
  apply plugin: it
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
}

pluginBundle {
  website = 'https://github.com/orctom/gradle-archetype-plugin'
  vcsUrl = 'https://github.com/orctom/gradle-archetype-plugin'

  description = 'Gradle plugin that generate projects from template.'
  tags = ['archetype', 'template']

  plugins {
    plugin {
      id = 'com.orctom.archetype'
      displayName = 'Archetype Generate Plugin'
    }
  }
}

task testJar(type: Jar, dependsOn: testClasses) {
  baseName = ARTIFACT
  version = VERSION
  classifier = 'tests'
  from sourceSets.test.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
  baseName = ARTIFACT
  version = VERSION
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName = ARTIFACT
  version = VERSION
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId MAVEN_GROUP
      artifactId ARTIFACT
      version VERSION

      from components.java

      artifact sourcesJar {
        classifier "sources"
      }

      artifact (javadocJar) {
        classifier = 'javadoc'
      }

      artifact (testJar) {
        classifier = 'tests'
      }
    }
  }
  repositories {
    maven {
      credentials {
        username System.getenv('NEXUS_USERNAME')
        password System.getenv('NEXUS_PASSWORD')
      }
      url "http://binary-repo.budgetdirect.com.au/repository/${VERSION.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
    }
  }
}
